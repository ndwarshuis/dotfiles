# Generated by gem2arch (https://github.com/anatol/gem2arch)
# Maintainer: Jose Riha <jose1711 gmail com>
# Contributor: mitro <mitro@somecode.com>

_pkgname=taskjuggler
pkgname=$_pkgname-git
_pkgver=3.6.0
pkgver=release.3.5.0.79.gf36b8f44
pkgrel=1
pkgdesc="A project management tool for Linux and UNIX system-based operating systems."
arch=(any)
url='http://www.taskjuggler.org'
license=('GPL')
depends=(ruby ruby-mail ruby-term-ansicolor ruby-rdoc)
makedepends=(ruby-rake ruby-rspec)
options=(!emptydirs)
source=('git://github.com/taskjuggler/TaskJuggler.git')
sha1sums=('SKIP')

pkgver() {
  cd TaskJuggler
  git describe | sed 's/-/./g'
}

build() {
  cd TaskJuggler
  rake gem
}

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i \
      "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" \
      "$srcdir/TaskJuggler/pkg/$_pkgname-$_pkgver.gem"
  rm "$pkgdir/$_gemdir/cache/$_pkgname-$_pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_pkgname-$_pkgver/COPYING" "$pkgdir/usr/share/licenses/$_pkgname/COPYING"
}
